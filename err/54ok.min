program
function void cruzes number x do
	if x > 50 then
		"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX...\n"!
	else
		if x > 0 then
			for 0 until x = 0 step x := x - 1 do "X"! done
		fi
	fi;
number seed := 100001;
function number random number lower; number upper do
	seed := (seed * 125) % 2796203;
	return seed % (upper-lower+1) + lower;
function forward number atoi string s done;
function forward number argc done;
function forward string argv number n done
start
	array tab;
	number lower;
	number upper;
	number slot;
	number rand;
	number val;
	number i;

	if argc < 4 then
		"USAGE: histograma lower-limit upper-limit step [random]\n"!
		return 1
	fi
	lower := atoi(argv(1));
	upper := atoi(argv(2));
	if lower >= upper then
		"invalid argument: lower >= upper\n"!
		return 2
	fi
	slot := atoi(argv(3));
	if slot <= 0 then
		"invalid argument: step <= 0\n"!
		return 2
	fi
	val := (upper-lower+1)/slot;
	tab # val;
	for i := 0 until i = val step i := i + 1 do tab[i] := 0; done
	if argc > 4 then
		rand := atoi(argv(4));
		if rand <= 0 then
			"invalid argument: rand <= 0\n"!
			return 2
		fi
		for 0 until rand = 0 step rand := rand - 1 do
			i := random(lower, upper);
			tab[(i-lower)/slot] := tab[(i-lower)/slot] + 1;
		done
	else
		val := ?;
		for val := ? until val < lower | val > upper step val := ? do
			tab[(val-lower)/slot] := tab[(val-lower)/slot] + 1;
		done
	fi
	for val := lower until val > upper step val := val + slot do
		tab[(val-lower)/slot]!  " de "!  val!  " a "!  val+slot-1!
		": "!  cruzes(tab[(val-lower)/slot]); "\n"!
	done
end
